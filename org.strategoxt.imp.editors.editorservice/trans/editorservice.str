module editorservice

// TODO: warn about multi-char highlighted brackets
// TODO: check if provider/imported/etc. files exist
// TODO: check if there is _any_ provider for observer etc.

imports
  libstratego-lib  
  libstratego-sglr
  libstratego-sdf
  lib/editor-common.generated
  include/EditorService
  check
  analyze
  complete
  editor/EditorService-Outliner
  lib/runtime/editor/-

rules
  
  editor-resolve:
    (selected, position, ast, path, project-path) -> target
    where
      <?KeyCombination(_)> selected;
      target := <KeyBindingDeclaration; ?KeyBinding(<id>, _)> selected

  editor-hover:
    (selected, position, ast, path, project-path) -> <try(origin-text)> target
    where
      <?KeyCombination(_)> selected;
      target := <KeyBindingDeclaration; ?KeyBinding(_, <id>)> selected
      
  editor-outline:
    (node, position, ast, path, project-path) -> outline
    where
      outline := <simple-label-outline(to-outline-label; try(un-double-quote))> ast
      
  editor-analyze:
    (ast, path, project-path) -> (ast, error*, warning*, info*)
    with
      <chdir> project-path;
      record-all-sdfs;
      record-all-esvs;
      error*   := <collect-all(constraint-error, conc)> ast;
      warning* := <collect-all(constraint-warning, conc)> ast;
      info* := <collect-all(constraint-info, conc)> ast

  generate-aterm:
    (selected, position, ast, path, project-path) -> (filename, selected)
    where
      filename  := <guarantee-extension(|"aterm")> path

  
